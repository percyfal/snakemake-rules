# -*- snakemake -*-
include: "bwa.settings"
include: "bwa_index.rule"

config_default = {
    'bwa': {
        'aln' : {
	    'threads' : config['bwa']['threads'],
            'options': "",
            'runtime': "01:00:00",
        },
        'samse' : {
            'options' : "",
        },
    },
}

update_config(config_default, config)
config = config_default


rule bwa_aln_samse:
    """Run bwa aln followed by samse; outputs bam"""
    params: options = config['bwa']['aln']['options'],
            index = config['bwa']['index'],
            runtime = config['bwa']['aln']['runtime'],
            options_samse = config['bwa']['samse']['options']
    input: read = "{prefix}" + config['ngs.settings']['read1_label'] + config['ngs.settings']['fastq_suffix'],
           index = expand("{index}{ext}", index=config['bwa']['index'], ext=config['bwa']['index_ext'])
    output: bam = "{prefix}.bam"
    log: log = "{prefix}.bwa.log"
    threads: config['bwa']['aln']['threads']
    shell:
        "bwa aln -t {threads} {params.options} {params.index} " + \
        "{input.read} 2> {log} | bwa samse {params.options_samse} {params.index} - {input.read}" + \
        "| samtools view -b - > {output.bam}"

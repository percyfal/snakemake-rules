# -*- snakemake -*-
import shutil
from snakemake.exceptions import RuleException

def _picard_merge_sam_input_fn(wildcards):
    """Function to find input files for picard merge.

    Utilise information in config['settings']['sampleinfo'],
    config['settings']['runfmt'] and config['settings']['samplefmt'].
    'runfmt' and 'samplefmt' are python miniformat strings. The
    formatted runfmt string is used to generate names of input bam
    files. The current wildcard prefix corresponds to a formatted
    samplefmt string, all of which are computed and compared to the
    former.
    """
    try:
        suffix = config['bio.ngs.qc.picard']['merge_sam']['source_suffix']
        runfmt = config['settings']['runfmt']
        samplefmt = config['settings']['samplefmt']
        samplepfx = {k:k for k in set(samplefmt.format(**s) for s in config['_sampleinfo'])}
        try:
            current_sample = samplepfx[wildcards.prefix]
        except KeyError:
            raise Exception("no such sample prefix {}; sampleinfo file malformatted?".format(wildcards.prefix))
        inputset = list(set(runfmt.format(**x) + suffix for x in config['_sampleinfo'] if samplefmt.format(**x) == current_sample))
        return inputset
    except:
        raise RuleException("""
        picard_merge_sam: Default merge function failed. Make sure the source
        suffix config['bio.ngs.qc.picard']['merge_sam']['source_label'] is
        correct. Else, implement function for generating source files and
        set configuration
        config['bio.ngs.qc.picard']['merge_sam']['inputfun'] to the
        function. Alternatively, use
        workflow.rules.picard_merge_sam.set_input(function) to set the
        input function. The function should return a list of bam files to
        be merged""")


config_default = {
    'bio.ngs.qc.picard' : {
        'merge_sam' : {
            'label' : "merge", # target label
            'source_suffix' : ".bam",
            'options' : "CREATE_INDEX=true",
            'inputfun': _picard_merge_sam_input_fn,
        },
    },
}

update_config(config_default, config)
config = config_default


rule picard_merge_sam:
    """Picard: merge sam files.

    NB: always outputs bam files!
    """
    params: cmd = config['bio.ngs.qc.picard']['cmd'] + MERGE_SAM_FILES,
            options = " ".join([config['bio.ngs.qc.picard']['options'],
                                config['bio.ngs.qc.picard']['merge_sam']['options']])
    input: config['bio.ngs.qc.picard']['merge_sam']['inputfun']
    output: merge="{prefix}." + config['bio.ngs.qc.picard']['merge_sam']['label'] + ".bam"
    run: 
      if (len(input) > 1):
          inputstr = " ".join(["INPUT={}".format(x) for x in input])
          shell("{cmd} {ips} OUTPUT={out} {opt}".format(cmd=params.cmd, ips=inputstr, out=output.merge, opt=params.options))
      else:
          if os.path.exists(output.merge):
              os.unlink(output.merge)
          shutil.copy(input[0], output.merge)

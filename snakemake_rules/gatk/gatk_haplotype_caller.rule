# -*- snakemake -*-
include: 'gatk.settings'
include: '../picard/picard_build_bam_index.rule'

config_default = {
    'gatk' : {
        'haplotype_caller' : {
            'cmd' : 'HaplotypeCaller',
            'ref' : config['gatk']['ref'],
            'options' : '-ERC GVCF',
            'runtime' : config['gatk']['runtime'],
            'java_mem' : config['gatk']['java_mem'],
        },
    },
}

update_config(config_default, config)
config = config_default

cmd = re.sub("-Xmx[0-9a-zA-Z]+", "-Xmx{mem}".format(mem=config['gatk']['haplotype_caller']['java_mem']), config['gatk']['cmd'])

rule gatk_haplotype_caller:
    """Run GATK HaplotypeCaller"""
    params: cmd = cmd + " -T " + config['gatk']['haplotype_caller']['cmd'],
            options = config['gatk']['haplotype_caller']['options'],
            runtime = config['gatk']['haplotype_caller']['runtime'],
    input: bam = "{prefix}.bam", bai = "{prefix}.bai", ref = config['gatk']['haplotype_caller']['ref'], refidx = config['gatk']['haplotype_caller']['ref'] + ".fai" 
    output: gvcf = "{prefix}.g.vcf"
    threads: 1
    conda: "env.yaml"
    log: "{prefix}.g.vcf.log"
    shell: "{params.cmd} {params.options} -I {input.bam} -R {input.ref} -o {output.gvcf} -log {log}"


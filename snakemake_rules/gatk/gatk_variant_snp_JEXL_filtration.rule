# -*- snakemake -*-
include: 'gatk.settings'
include: 'gatk_select_snp_variants.rule'

config_default = {
    'gatk' : {
        'variant_snp_JEXL_filtration' : {
            'cmd' : 'VariantFiltration',
            'ref' : config['gatk']['ref'],
            'expressions' : ["QD < 2.0", "MQ < 40.0", "FS > 60.0",
                             "HaplotypeScore > 13.0", "MQRankSum < -12.5",
                             "ReadPosRankSum < -8.0"],
            'runtime' : config['gatk']['runtime'],
        },
    },
}

update_config(config_default, config)
config = config_default


rule gatk_variant_snp_JEXL_filtration:
    """Run GATK VariantFiltration on SNPs

    Perform hard filtering using JEXL expressions.
    """
    params: cmd = config['gatk']['cmd'] + " -T " + config['gatk']['variant_snp_JEXL_filtration']['cmd'],
            options = " ".join([
                " ".join(["--filterName GATKStandard{e} --filterExpression \"{exp}\"".format(e=exp.split()[0], exp=exp) \
                                for exp in config['gatk']['variant_snp_JEXL_filtration']['expressions']])
                ]),
            quote = "'" if config['gatk']['cmd'].startswith("gatk") else "",
            runtime = config['gatk']['variant_snp_JEXL_filtration']['runtime']
    input: vcf = "{prefix}.vcf", ref = config['gatk']['variant_snp_JEXL_filtration']['ref']
    output: vcf = "{prefix}.filteredSNP.vcf"
    threads: 1
    conda: "env.yaml"
    shell: "{params.cmd} {params.quote}{params.options}{params.quote} -R {input.ref} --variant {input.vcf} --out {output.vcf}"

